plugins {
    alias libs.plugins.android.application
    alias libs.plugins.navigation.safeargs
    alias libs.plugins.kotlin.android
    alias libs.plugins.hilt.android
    alias libs.plugins.devtools.ksp
    id 'kotlin-kapt'
    id("kotlin-parcelize")
}

android {
    namespace 'com.yatik.qrscanner'
    compileSdk 34

    defaultConfig {
        applicationId "com.yatik.qrscanner"
        minSdk 23
        targetSdk 34
        versionCode 36
        versionName "2.6.2"

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner = "com.yatik.qrscanner.HiltTestRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += file("$projectDir/schemas".toString())
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }

    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {

    implementation libs.preference.ktx
    implementation libs.core.ktx
    implementation libs.activity.ktx
    implementation libs.runner

    implementation libs.appcompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.guava

    // Navigation components
    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx

    // CameraX components
    implementation libs.camera.core
    implementation libs.camera.camera2
    implementation libs.camera.view
    implementation libs.camera.lifecycle

    // Google Barcode scanning ML-kit
    implementation libs.barcode.scanning

    // Fragments
    implementation libs.fragment.ktx
    debugImplementation libs.fragment.testing

    // Coordinator Layout
    implementation libs.coordinatorlayout

    // Room components
    implementation libs.room.ktx
    ksp libs.room.compiler
    annotationProcessor libs.room.compiler
    androidTestImplementation libs.room.testing

    // Lifecycle components
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.common.java8

    // Kotlin components
    implementation libs.kotlin.stdlib.jdk7
    api libs.kotlinx.coroutines.core
    api libs.kotlinx.coroutines.android

    // Dagger Hilt
    implementation libs.hilt.android
    kapt libs.hilt.android.compiler

    // Retrofit
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.logging.interceptor

    //Custom Chrome Tabs
    implementation libs.browser

    // Paging Components
    implementation libs.paging.runtime.ktx
    implementation libs.room.paging
    testImplementation libs.paging.common.ktx

    // To test coroutines
    testImplementation libs.kotlinx.coroutine.test
    androidTestImplementation libs.kotlinx.coroutine.test

    testImplementation libs.junit
    testImplementation libs.core.testing
    //noinspection GradleDependency
    androidTestImplementation libs.core
    androidTestImplementation libs.test.ext.junit
    androidTestImplementation libs.core.testing
    androidTestImplementation libs.espresso.core
    //noinspection GradleDependency
    androidTestImplementation libs.espresso.contrib
    androidTestImplementation libs.espresso.intents

    // zxing for QR code generation
    implementation libs.zxing

    // Jsoup to parse HTML response
    implementation libs.jsoup

    // Coil to fetch and display images
    implementation libs.coil

    // Shimmer effect
    implementation libs.shimmer

    // CanHub image cropper
    implementation libs.android.image.cropper

    // Turbine to test Flow
    testImplementation libs.turbine
    androidTestImplementation libs.turbine

    // Truth library for assertions
    testImplementation libs.truth
    androidTestImplementation libs.truth

    // Hilt for testing
    debugImplementation libs.hilt.android.testing
    kaptAndroidTest libs.hilt.android.compiler

    // MockWebServer
    testImplementation libs.mockwebserver

    // Mockito
    testImplementation libs.mockito.core
    testImplementation libs.mockito.inline
    androidTestImplementation libs.mockito.android
}
